\chapter{Full EBNF for PHAL}\label{BNF:BNF}
\begin{table}[H]
\centering
\begin{tabular}{@{}lc@{}}
\toprule
\textbf{Terminal}   & \multicolumn{1}{l}{\textbf{Regular expression}} \\ \midrule
value            & [1-9][0-9]*.[0-9]*[1-9]                            \\
id               & [a-zA-Z\_][a-zA-Z0-9\_]*                 \\
text             & [$\psi$]* $\cap$ $"$ $\cap$ $null$                                \\
number           & [1-9][0-9]*.[0-9]*[1-9]                            \\
bool             & true $\cup$ false $\cup$ on $\cup$ off   \\
\bottomrule
\end{tabular}
\caption{Regular expressions for terminals}
\label{table:Tokens}
\end{table}
\noindent
Where $\psi$ is the set of all UTF-8 characters.

\begin{lstlisting}[caption={EBNF for PHAL}, label={BFG:BNF}, escapeinside={(*}{*)}]
Program     (*$\xrightarrow{}$*)  {NEWLINE} {include} {NEWLINE} 
                Setup {NEWLINE} Repeat {NEWLINE} 
                {Func} {NEWLINE}

Include	    (*$\xrightarrow{}$*)  'using' id NEWLINE+

Setup       (*$\xrightarrow{}$*)  'setup' {NEWLINE} '{' 
                {NEWLINE} {SetupCnt} {NEWLINE} 
                '}'

SetupCnt    (*$\xrightarrow{}$*)  Dcl NEWLINE+ | Stmt 

Dcl 	    (*$\xrightarrow{}$*)  VarDcl | CmpDcl   
                | AdvDataType

VarDcl	    (*$\xrightarrow{}$*)  Type id | Type id ':=' Expr

Type	    (*$\xrightarrow{}$*)  'number' | 'text' | 'bool'
                | 'list' | 'group' | AdvDataType
            
AdvDataType (*$\xrightarrow{}$*)  Group | List 

CmpDcl	    (*$\xrightarrow{}$*)  AdvType id ':=' 'pin' number ( ',' number)*

AdvType	    (*$\xrightarrow{}$*)  'lightbulb' | 'motor'  
                | 'temperatureSensor'

Group	    (*$\xrightarrow{}$*)  'group' id {NEWLINE} '{' 
                {NEWLINE} (id NEWLINE+)+ 
                '}'

List	    (*$\xrightarrow{}$*)  'list' Type id '{' (Expr { ',' Expr})? '}'

Stmt	    (*$\xrightarrow{}$*)  Selective NEWLINE+ | Iterative NEWLINE+ 
                | FuncCall  NEWLINE+ | Assignment NEWLINE+  
                | ReturnStmt NEWLINE+ | WaitStmt NEWLINE+  
                | AdvTypeModifier NEWLINE+

WaitStmt    (*$\xrightarrow{}$*)  'wait' Expr 'seconds'

Selective   (*$\xrightarrow{}$*)  Switch | IfStmt

Switch	    (*$\xrightarrow{}$*)  'switch' '(' Expr ')' {NEWLINE} '{' 
                {NEWLINE} CaseList 
                '}'

CaseList    (*$\xrightarrow{}$*)  CaseStmt+ DefaultCase

CaseStmt    (*$\xrightarrow{}$*)  'case' Expr ':' {NEWLINE} {Stmt} 

DefaultCase (*$\xrightarrow{}$*) 'default' ':' {NEWLINE} {Stmt}

IfStmt	    (*$\xrightarrow{}$*) 'if' '(' Expr ')' 'then' {NEWLINE} '{' 
                {NEWLINE} block {NEWLINE} 
                '}' {NEWLINE} 
                {'else if' '(' Expr ')' 'then' {NEWLINE} '{' 
                {NEWLINE} block {NEWLINE} 
                '}'} 
                {NEWLINE} ('else' 'then' {NEWLINE} '{' 
                {NEWLINE} block {NEWLINE} 
                '}')? 

Block       (*$\xrightarrow{}$*)  {Stmt}
            
Condition   (*$\xrightarrow{}$*)  id LogicOper  LogicalStmt  
                | id LogicOper id  
                | id LogicOper value  
                | value LogicOper LogicalStmt
                | value LogicOper id | value LogicOper value  
                | id | boolValue

Iterative   (*$\xrightarrow{}$*)  LoopTimes | LoopUntil

LoopTimes   (*$\xrightarrow{}$*) 'loop' Expr 'times' {NEWLINE} '{' 
                {NEWLINE} {Stmt} {NEWLINE} 
                '}'

LoopUntil   (*$\xrightarrow{}$*) 'loop' 'until' Expr 
                ( ',' ('increase' | 'decrease') id 'by' number)? 
                {NEWLINE} '{' {NEWLINE} {Stmt} {NEWLINE} '}'  

FuncCall    (*$\xrightarrow{}$*)  'call' id 'with' '(' (none | CallCnt) ')'

CallCnt	    (*$\xrightarrow{}$*)  Expr {',' Expr}


Assignment  (*$\xrightarrow{}$*)  id('.'id)? ':=' Expr | id('.'id)? '+=' Expr  
                | id('.'id)? '-=' Expr

AdvTypeModifier (*$\xrightarrow{}$*)  'add' Expr {',' Expr} 'to' id  
                | 'remove' 'element' Expr {',' Expr} 'from' id

Repeat      (*$\xrightarrow{}$*)  'repeat' {NEWLINE} '{' 
                {NEWLINE} {Stmt} {NEWLINE} 
                '}' 

Func	    (*$\xrightarrow{}$*)  'define' id 'with' '(' Parameters ')' 
                'returnType' (Type | 'none') {NEWLINE} '{' 
                {NEWLINE} {FuncCnt} {NEWLINE}
                '}'

FuncCnt     (*$\xrightarrow{}$*)  VarDcl {NEWLINE} | Stmt {NEWLINE}  
                | List NEWLINE+

Parameters  (*$\xrightarrow{}$*)  Param { ',' Param} | 'none'

Param	    (*$\xrightarrow{}$*)  Type id

ReturnStmt  (*$\xrightarrow{}$*)  'return' Expr

Expr        (*$\xrightarrow{}$*) bool  |  number  |  id  |  text  |  id'.'id  
                | FuncCall | '(' Expr')'  
                | ('!' | 'not') Expr | '-' Expr
                | Expr ('+'|'-') Expr 
                | Expr ('*'|'/'|'\%') Expr  
                | Expr ('!='|'='| 'is' | 'is not') Expr
                | Expr ('<'|'>'|'less than'|'greater than') Expr
                | Expr ('<='|'less than or equal to') Expr
                | Expr ('>=' |'greater than or equal to') Expr 
                | Expr ('and'|'&') Expr  
                | Expr ('or'|'|') Expr
                | 'get' 'element' Expr {',' Expr} 'from' id

Comment     (*$\xrightarrow{}$*) SingleLine | MultiLine

SingleLine  (*$\xrightarrow{}$*) '#' (letter | digit) NEWLINE+

MultiLine   (*$\xrightarrow{}$*)  '/*' text '*/' {NEWLINE}

NEWLINE	    (*$\xrightarrow{}$*)  ['\r'] '\n'
\end{lstlisting}